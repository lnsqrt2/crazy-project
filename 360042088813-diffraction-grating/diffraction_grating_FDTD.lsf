closeall; clear;

# get results from the analysis groups
mname="grating_T";
T=getresult(mname,"T");
T_grating=getresult(mname,"T_grating");
T_num_order=getresult(mname,"num_orders");
mname="grating_R";
R=getresult(mname,"T");
R_grating=getresult(mname,"T_grating");
R_num_order=getresult(mname,"num_orders");
f=T.f;

################################################
# 1. Number of diffraction orders vs. wavelength
################################################

plot(T.lambda*1e6,T_num_order.num_orders,R_num_order.num_orders,"wavelength (um)","Number of supported orders","","linewidth = 2");
setplot("x min",0.85);setplot("x max",1);
legend("Transmission","Reflection");

#############################################################################################
# 2. Fraction of transmitted/reflected power into a specific diffraction order vs. wavelength
#############################################################################################

# (0,0) order
n_target = 0; 
m_target = 0;

T_00 = pinch( pinch(T_grating.T_grating,1,find(T_grating.n,n_target)) ,1,find(T_grating.m,m_target));
R_00 = pinch( pinch(R_grating.T_grating,1,find(R_grating.n,n_target)) ,1,find(R_grating.m,m_target));
plot(c/T.f*1e6,T.T,T_00,-R.T,R_00,-R.T+T.T,"Wavelength (um)","Transmission","","linewidth=2");
legend("T Total",
       "T ("+num2str(n_target)+","+num2str(m_target)+")",
       "R Total",
       "R ("+num2str(n_target)+","+num2str(m_target)+")",
       "R + T");
setplot("x min",0.85);setplot("x max",1);

#########################################################################################
# 3. Diffraction angle for a specific diffraction order vs. wavelength
#########################################################################################

# (0,1) order
n_target = 0; 
m_target = 1;
theta_01 = pinch( pinch(T_grating.theta,1,find(T_grating.n,n_target)) ,1,find(T_grating.m,m_target));
phi_01   = pinch( pinch(T_grating.phi,  1,find(T_grating.n,n_target)) ,1,find(T_grating.m,m_target));

plot(c/T.f*1e6,theta_01,phi_01,"Wavelength (um)","Diffraction angle for "+num2str(n_target)+","+num2str(m_target)+") order","","linewidth=2");
legend("Theta","Phi");
setplot("x min",0.85);setplot("x max",1);
setplot("y min",70);setplot("y max",91);

#####################################################################################################
# 4. Diffraction efficincies and angles for all supported diffraction orders at a specific wavelength
#####################################################################################################

target_wavelength = 0.85e-6;
fi = find(f,c/target_wavelength);

?"Transmitted orders in u1(x) direction at " + num2str(target_wavelength*1e6) + " um : " + num2str(transpose(T_grating.n));
?"Transmitted orders in u2(y) direction at " + num2str(target_wavelength*1e6) + " um : " + num2str(transpose(T_grating.m));
?"Reflected orders in u1(x) direction at " + num2str(target_wavelength*1e6) + " um : " + num2str(transpose(R_grating.n));
?"Reflected orders in u2(y) direction at " + num2str(target_wavelength*1e6) + " um : " + num2str(transpose(R_grating.m));

T_nm = pinch(T_grating.T_grating,3,fi);
theta_T_nm = pinch(T_grating.theta,3,fi);
phi_T_nm = pinch(T_grating.phi,3,fi);

R_nm = pinch(R_grating.T_grating,3,fi);
theta_R_nm = pinch(R_grating.theta,3,fi);
phi_R_nm = pinch(R_grating.phi,3,fi);

# polar image of diffraction orders
res = 25;   # Number of points for the polar image
            # Note: grating orders will appear as a point in the polar plot.
            # Setting this value too large might result in the grating orders unrecognizable
T_nm_new = matrix(res,res)+1e-18;
u1 = pinch( pinch(T_grating.U1,3,fi) ,2,1);
u2 = pinch( pinch(T_grating.U2,3,fi) ,1,1);
u1_new = linspace(-1,1,res);
u2_new = linspace(-1,1,res);
u1i = find(u1_new,u1);
u2i = find(u2_new,u2);
T_nm_new(u1i,u2i) = T_nm;  

image(u1_new,u2_new,log10(T_nm_new),"u1","u2","Transmitted orders at "+num2str(c/f(fi)*1e6)+"um (log scale)","polar plot");
setplot("colorbar min",-5);setplot("colorbar max",0);

R_nm_new = matrix(res,res)+1e-18;
u1 = pinch( pinch(R_grating.U1,3,fi) ,2,1);
u2 = pinch( pinch(R_grating.U2,3,fi) ,1,1);
u1_new = linspace(-1,1,res);
u2_new = linspace(-1,1,res);
u1i = find(u1_new,u1);
u2i = find(u2_new,u2);
R_nm_new(u1i,u2i) = R_nm;  

image(u1_new,u2_new,log10(R_nm_new),"u1","u2","Reflected orders at "+num2str(c/f(fi)*1e6)+"um (log scale)","polar plot");
setplot("colorbar min",-5);setplot("colorbar max",0);